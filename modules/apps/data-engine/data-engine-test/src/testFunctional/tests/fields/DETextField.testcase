@component-name = "data-engine"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Data Engine";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONGroup.addGroup(groupName = "Test Site Name");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "This is a test for LRQA-68539. This test verifies that is possible to delete the field."
	@priority = "5"
	test CanBeDeleted {
		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Title");

		DataEngine.addField(
			fieldFieldLabel = "Text",
			fieldName = "Text");

		DataEngine.addField(
			fieldFieldLabel = "Numeric",
			fieldName = "Numeric");

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Title");

		DataEngine.delete(fieldFieldLabel = "Text");

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Title");

		DataEngine.assertFieldNotPresent(fieldFieldLabel = "Text");
	}

	@description = "This is a test for LRQA-68539. This test verifies that is possible to duplicate the field."
	@priority = "4"
	test CanBeDuplicated {
		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Title");

		DataEngine.addField(
			fieldFieldLabel = "Text",
			fieldName = "Text");

		DataEngine.duplicate(fieldFieldLabel = "Text");

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Title");

		DataEngine.viewField(
			fieldFieldLabel = "Text",
			fieldName = "Text");

		DataEngine.viewField(
			fieldFieldLabel = "Copy of Text",
			fieldName = "Text");
	}

	@description = "This is a test for LRQA-68539. This test verifies that Label, Placeholder and Help text can be edited."
	@priority = "5"
	test CanEditLabelPlaceholderAndHelpText {
		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Title");

		DataEngine.addField(
			fieldFieldLabel = "Text",
			fieldName = "Text");

		DataEngine.editFieldTip(
			fieldFieldLabel = "Text",
			fieldHelp = "Help Text");

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Title");

		DataEngine.editFieldLabel(
			fieldFieldLabel = "Text",
			fieldFieldLabelEdit = "Text Edited");

		DataEngine.editFieldTip(
			fieldFieldLabel = "Text Edited",
			fieldHelp = "Help Text Edited");

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Title");

		DataEngine.viewField(
			fieldFieldLabel = "Text Edited",
			fieldHelp = "Help Text Edited",
			fieldName = "Text");
	}

	@description = "This is a test for LRQA-68539. This test verifies that Text field can be set as Single line or Multiple line."
	@priority = "3"
	test CanBeSingleOrMultipleLine {
		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Title");

		DataEngine.addField(
			fieldFieldLabel = "Text",
			fieldName = "Text");

		DataEngine.editFieldType(
			fieldFieldLabel = "Text",
			fieldlineType = "Multiple Lines");

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Title");

		DataEngine.viewFieldType(
			fieldFieldLabel = "Text",
			fieldlineType = "Multiple Lines");

		DataEngine.editFieldType(
			fieldFieldLabel = "Text",
			fieldlineType = "Single Line");

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Title");

		DataEngine.viewFieldType(
			fieldFieldLabel = "Text",
			fieldlineType = "Single Line");
	}

	@description = "This is a test for LRQA-68539. This test verifies that Default Searchable property is 'Disable' when System Setting is left unchecked."
	@priority = "3"
	test CanSetDefaultSearchablePropertyToDisable {
		ApplicationsMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "System Settings");

		SystemSettings.gotoConfiguration(
			configurationCategory = "Web Content",
			configurationName = "Administration",
			configurationScope = "System Scope");

		FormFields.disableCheckbox(fieldName = "Structure Fields Indexable Enable");

		SystemSettings.saveConfiguration();

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Title");

		DataEngine.addField(
			fieldFieldLabel = "Text",
			fieldName = "Text");

		DataEngine.viewFieldSearchable(
			fieldFieldLabel = "Text",
			searchableType = "Disable");
	}

	@description = "This is a test for LRQA-68539. This test verifies that Default Searchable property is 'Keyword' when System Setting is checked."
	@priority = "3"
	test CanSetDefaultSearchablePropertyToKeyword {
		ApplicationsMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "System Settings");

		SystemSettings.gotoConfiguration(
			configurationCategory = "Web Content",
			configurationName = "Administration",
			configurationScope = "System Scope");

		FormFields.viewCheckboxChecked(fieldName = "Structure Fields Indexable Enable");

		SystemSettings.saveConfiguration();

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Title");

		DataEngine.addField(
			fieldFieldLabel = "Text",
			fieldName = "Text");

		DataEngine.viewFieldSearchable(
			fieldFieldLabel = "Text",
			searchableType = "Keyword");
	}

	@description = "This is a test for LRQA-68539. This test verifies that Label, Placeholder and Help text can be set."
	@priority = "5"
	test CanSetLabelPlaceholderAndHelpText {
		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Title");

		DataEngine.addField(
			fieldFieldLabel = "Text",
			fieldName = "Text");

		DataEngine.editFieldTip(
			fieldFieldLabel = "Text",
			fieldHelp = "Help Text",
			fieldPlaceholder = "Placeholder Text");

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Title");

		DataEngine.viewField(
			fieldFieldLabel = "Text",
			fieldHelp = "Help Text",
			fieldName = "Text",
			fieldPlaceholder = "Placeholder Text");
	}

	@description = "This is a test for LRQA-68539. This test verifies that Label can be shown or hidden."
	@priority = "3"
	test CanSetLabelToShownOrHidden {
		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Title");

		DataEngine.addField(
			fieldFieldLabel = "Text",
			fieldName = "Text");

		DataEngine.toggleShowLabel(fieldLabel = "Text");

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Title");

		DataEngine.assertFieldNotPresent(fieldFieldLabel = "Text");

		DataEngine.toggleShowLabel();

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Title");

		DataEngine.viewField(
			fieldFieldLabel = "Text",
			fieldName = "Text");
	}

}