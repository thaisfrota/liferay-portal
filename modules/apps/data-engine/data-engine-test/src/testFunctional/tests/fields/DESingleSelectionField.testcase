@component-name = "data-engine"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Data Engine";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONGroup.addGroup(groupName = "Test Site Name");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "This is a test for LRQA-68571. This test verifies that is possible to delete the field."
	@priority = "5"
	test CanBeDeleted {
		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Title");

		DataEngine.addField(
			fieldFieldLabel = "Single Selection",
			fieldName = "Single Selection");

		DataEngine.addField(
			fieldFieldLabel = "Text",
			fieldName = "Text");

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Title");

		DataEngine.delete(fieldFieldLabel = "Single Selection");

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Title");

		DataEngine.assertFieldNotPresent(fieldFieldLabel = "Single Selection");
	}

	@description = "This is a test for LRQA-68571. This test verifies that is possible to duplicate the field."
	@priority = "4"
	test CanBeDuplicated {
		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Title");

		DataEngine.addField(
			fieldFieldLabel = "Single Selection",
			fieldName = "Single Selection");

		DataEngine.duplicate(fieldFieldLabel = "Single Selection");

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Title");

		DataEngine.viewField(
			fieldFieldLabel = "Single Selection",
			fieldName = "Single Selection");

		DataEngine.viewField(
			fieldFieldLabel = "Copy of Single Selection",
			fieldName = "Single Selection");
	}

	@description = "This is a test for LRQA-68571. This test verifies that is possible to change inline layout setting."
	@priority = "3"
	test CanBeInlined {
		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Title");

		DataEngine.addField(
			fieldFieldLabel = "Single Selection",
			fieldName = "Single Selection");

		for (var index : list "1,2,3") {
			DataEngine.addFieldOption(
				fieldFieldLabel = "Single Selection",
				index = "${index}",
				optionValue = "Option ${index}");
		}

		DataEngine.toggleInline(fieldLabel = "Single Selection");

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Title");

		DataEngine.assertNonInlineOptions(
			fieldLabel = "Single Selection",
			optionLabelList = "Option 1,Option 2,Option 3");

		DataEngine.toggleInline(fieldLabel = "Single Selection");

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Title");

		DataEngine.assertInlineOptions(
			fieldLabel = "Single Selection",
			optionLabelList = "Option 1,Option 2,Option 3");
	}

	@description = "This is a test for LRQA-68571. This test verifies that Label and Help text can be edited."
	@priority = "5"
	test CanEditLabelAndHelpText {
		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Title");

		DataEngine.addField(
			fieldFieldLabel = "Single Selection",
			fieldName = "Single Selection");

		DataEngine.editFieldTip(
			fieldFieldLabel = "Single Selection",
			fieldHelp = "Help Text");

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Title");

		DataEngine.editFieldLabel(
			fieldFieldLabel = "Single Selection",
			fieldFieldLabelEdit = "Single Selection Edited");

		DataEngine.editFieldTip(
			fieldFieldLabel = "Single Selection Edited",
			fieldHelp = "Help Text Edited");

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Title");

		DataEngine.viewField(
			fieldFieldLabel = "Single Selection Edited",
			fieldHelp = "Help Text Edited",
			fieldName = "Single Selection");
	}

	@description = "This is a test for LRQA-69037. This test verifies that chosen options persists."
	@priority = "5"
	test CanSelectOptions {
		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Title");

		DEBuilder.addField(
			fieldLabel = "Single Selection",
			fieldName = "Single Selection");

		DEBuilder.addFieldOptions(
			fieldLabel = "Single Selection",
			optionValueList = "Option 1,Option 2,Option 3");

		WebContentStructures.saveCP();

		NavItem.gotoWebContent();

		WebContentNavigator.gotoAddWithStructureCP(structureName = "WC Structure Title");

		PortletEntry.inputTitle(title = "Web Content Title");

		DERenderer.inputDataInSingleSelectionField(
			fieldLabel = "Single Selection",
			optionValue = "Option 1");

		Button.clickPublish();

		WebContentNavigator.gotoEditCP(webContentTitle = "Web Content Title");

		DERenderer.assertDataInSingleSelectionField(
			fieldLabel = "Single Selection",
			optionValue = "Option 1");
	}

	@description = "This is a test for LRQA-68571. This test verifies that Default Searchable property is 'Disable' when System Setting is left unchecked"
	@priority = "3"
	test CanSetDefaultSearchablePropertyToDisable {
		property test.name.skip.portal.instance = "DESingleSelectionField#CanSetDefaultSearchablePropertyToDisable";

		ApplicationsMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "System Settings");

		SystemSettings.gotoConfiguration(
			configurationCategory = "Web Content",
			configurationName = "Administration",
			configurationScope = "System Scope");

		FormFields.disableCheckbox(fieldName = "Structure Fields Indexable Enable");

		SystemSettings.saveConfiguration();

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Title");

		DataEngine.addField(
			fieldFieldLabel = "Single Selection",
			fieldName = "Single Selection");

		DataEngine.viewFieldSearchable(
			fieldFieldLabel = "Single Selection",
			searchableType = "Disable");
	}

	@description = "This is a test for LRQA-68571. This test verifies that Default Searchable property is 'Keyword' when System Setting is checked"
	@priority = "3"
	test CanSetDefaultSearchablePropertyToKeyword {
		property test.name.skip.portal.instance = "DESingleSelectionField#CanSetDefaultSearchablePropertyToKeyword";

		ApplicationsMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "System Settings");

		SystemSettings.gotoConfiguration(
			configurationCategory = "Web Content",
			configurationName = "Administration",
			configurationScope = "System Scope");

		FormFields.enableCheckbox(fieldName = "Structure Fields Indexable Enable");

		SystemSettings.saveConfiguration();

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Title");

		DataEngine.addField(
			fieldFieldLabel = "Single Selection",
			fieldName = "Single Selection");

		DataEngine.viewFieldSearchable(
			fieldFieldLabel = "Single Selection",
			searchableType = "Keyword");
	}

	@description = "This is a test for LRQA-68571. This test verifies that Label and Help text can be set."
	@priority = "5"
	test CanSetLabelAndHelpText {
		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Title");

		DataEngine.addField(
			fieldFieldLabel = "Single Selection",
			fieldName = "Single Selection");

		DataEngine.editFieldTip(
			fieldFieldLabel = "Single Selection",
			fieldHelp = "Help Text");

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Title");

		DataEngine.viewField(
			fieldFieldLabel = "Single Selection",
			fieldHelp = "Help Text",
			fieldName = "Single Selection");
	}

	@description = "This is a test for LRQA-68571. This test verifies that Label can be shown or hidden."
	@priority = "3"
	test CanSetLabelToShownOrHidden {
		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Title");

		DataEngine.addField(
			fieldFieldLabel = "Single Selection",
			fieldName = "Single Selection");

		DataEngine.toggleShowLabel(fieldLabel = "Single Selection");

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Title");

		DataEngine.assertFieldNotPresent(fieldFieldLabel = "Single Selection");

		DataEngine.toggleShowLabel();

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Title");

		DataEngine.viewField(
			fieldFieldLabel = "Single Selection",
			fieldName = "Single Selection");
	}

	@description = "This is a test for LRQA-68571. This test verifies that multiple options can be set."
	@priority = "5"
	test CanSetMultipleOptions {
		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Title");

		DataEngine.addField(
			fieldFieldLabel = "Single Selection",
			fieldName = "Single Selection");

		for (var index : list "1,2,3") {
			DataEngine.addFieldOption(
				fieldFieldLabel = "Single Selection",
				index = "${index}",
				optionValue = "Option ${index}");
		}

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Title");

		DataEngine.viewField(
			fieldFieldLabel = "Single Selection",
			fieldName = "Single Selection");

		DEBuilder.assertFieldOptionLabels(
			fieldFieldLabel = "Single Selection",
			optionLabelList = "Option 1,Option 2,Option 3");
	}

}