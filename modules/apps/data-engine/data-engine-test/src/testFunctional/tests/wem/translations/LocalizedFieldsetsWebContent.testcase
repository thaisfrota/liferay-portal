@component-name = "data-engine"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Data Engine";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONGroup.addGroup(groupName = "Test Site Name");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "This is a test for LPS-123539. As Content Creator, I want to leverage the structures’ translations with fieldset when creating a new Web Content"
	@priority = "5"
	test CheckDefaultWebContentTitleOnWebContentList {
		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Name");

		DataEngine.addFieldsets(
			fieldsetLabel = "Basic Web Content",
			fieldsetName = "Basic Web Content");

		WebContentStructures.saveCP();

		NavItem.click(navItem = "Web Content");

		WebContentNavigator.gotoAddWithStructureCP(structureName = "WC Structure Name");

		WebContent.addCP(
			webContentContent = "Web Content Content",
			webContentTitle = "Web Content Title");

		PortletEntry.changeLocale(locale = "pt-BR");

		WebContent.addCP(
			webContentContent = "Conteúdo da web",
			webContentTitle = "Título do conteúdo da web");

		PortletEntry.publish();

		WebContent.viewTitle(webContentTitle = "Web Content Title");
	}

	@description = "This is a test for LPS-123539. As Content Creator, I want to leverage the structures’ translations with fildset when creating a new Web Content"
	@ignore = "true"
	@priority = "4"
	test CheckFieldsetSummaryLocalization {
		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoStructures();

		WebContentStructures.addCP(
			structureDescription = "Structure Description Test",
			structureName = "Structure FieldSet Test");

		DataEngine.addField(
			fieldFieldLabel = "Text",
			fieldName = "Text");

		WebContentStructures.saveCP(structureName = "Structure Name Test");

		WebContentStructures.addCP(
			structureDescription = "Structure Description Test",
			structureName = "Structure Name Test");

		DataEngine.addFieldsets(
			fieldsetLabel = "Structure FieldSet Test",
			fieldsetName = "Structure FieldSet Test");

		WebContentStructures.saveCP(structureName = "Structure Name Test");

		NavItem.click(navItem = "Web Content");

		WebContentNavigator.gotoAddWithStructureCP(structureName = "Structure Name Test");

		PortletEntry.inputTitle(title = "WC Title Test");

		PortletEntry.inputText(
			fieldLabel = "Text",
			text = "Text test");

		PortletEntry.changeLocaleSummary(locale = "pt_BR");

		PortletEntry.viewLocale(locale = "pt-BR");

		PortletEntry.inputText(
			fieldLabel = "Text",
			text = "Text test BR");

		PortletEntry.changeLocaleSummary(locale = "en-US");

		PortletEntry.publish();

		WebContentNavigator.gotoEditCP(webContentTitle = "WC Title Test");

		AssertAttributeValue(
			attribute1 = "value",
			key_fieldFieldLabel = "Text",
			locator1 = "WCEditWebContent#TEXT_INPUT",
			value1 = "Text test");

		PortletEntry.changeLocaleSummary(locale = "pt_BR");

		AssertAttributeValue(
			attribute1 = "value",
			key_fieldFieldLabel = "Text",
			locator1 = "WCEditWebContent#TEXT_INPUT",
			value1 = "Text test BR");
	}

	@description = "This is a test for LPS-123539. As Content Creator, I want to leverage the structures’ translations with fildset when creating a new Web Content"
	@priority = "4"
	test CheckLanguageSelector {
		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Name");

		DataEngine.createNewFieldset(
			fieldNameList = "Text",
			fieldsetName = "New Fieldset",
			localeList = "pt-BR");

		DataEngine.addFieldsets(fieldsetName = "New Fieldset");

		WebContentStructures.saveCP();

		Navigator.gotoNavTab(navTab = "Web Content");

		WebContentNavigator.gotoAddWithStructureCP(structureName = "WC Structure Name");

		AssertVisible(locator1 = "WCEditWebContent#SIDEBAR");

		PortletEntry.viewLocale(locale = "en-US");

		AssertVisible(
			key_fieldsetLabel = "New Fieldset",
			locator1 = "DDMEditStructure#FORM_FIELD_SETS_CONTAINER_LABEL");

		AssertVisible(
			key_fieldFieldLabel = "Text",
			locator1 = "DDMEditStructure#FORM_FIELD_CONTAINER_LABEL");

		PortletEntry.changeLocale(locale = "pt-BR");

		PortletEntry.viewLocale(locale = "pt-BR");

		AssertVisible(
			key_fieldsetLabel = "New Fieldset pt-BR",
			locator1 = "DDMEditStructure#FORM_FIELD_SETS_CONTAINER_LABEL");

		AssertVisible(
			key_fieldFieldLabel = "Text pt-BR",
			locator1 = "DDMEditStructure#FORM_FIELD_CONTAINER_LABEL");
	}

	@description = "This is a test for LPS-123539. As Content Creator, I want to leverage the structures’ translations with fildset when creating a new Web Content"
	@priority = "5"
	test CheckStructureTranslationOnWebContent {
		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Name");

		DataEngine.createNewFieldset(
			fieldHelp = "true",
			fieldNameList = "Text",
			fieldsetName = "New Fieldset",
			localeList = "pt-BR");

		DataEngine.addFieldsets(fieldsetName = "New Fieldset");

		WebContentStructures.saveCP();

		NavItem.click(navItem = "Web Content");

		WebContentNavigator.gotoAddWithStructureCP(structureName = "WC Structure Name");

		WebContent.addCP(webContentTitle = "Web Content Title");

		PortletEntry.publish();

		WebContentNavigator.gotoEditCP(webContentTitle = "Web Content Title");

		AssertTextEquals(
			locator1 = "TextInput#TITLE",
			value1 = "Web Content Title");

		AssertVisible(
			key_fieldsetLabel = "New Fieldset",
			locator1 = "DDMEditStructure#FORM_FIELD_SETS_CONTAINER_LABEL");

		AssertVisible(
			key_fieldFieldLabel = "Text",
			locator1 = "DDMEditStructure#FORM_FIELD_CONTAINER_LABEL");

		AssertTextEquals(
			key_fieldFieldLabel = "Text",
			locator1 = "DDMEditStructure#SETTINGS_TEXT",
			value1 = "Text Help Text");

		PortletEntry.changeLocale(locale = "pt-BR");

		AssertTextEquals(
			locator1 = "TextInput#TITLE",
			value1 = "Web Content Title");

		AssertVisible(
			key_fieldsetLabel = "New Fieldset pt-BR",
			locator1 = "DDMEditStructure#FORM_FIELD_SETS_CONTAINER_LABEL");

		AssertVisible(
			key_fieldFieldLabel = "Text pt-BR",
			locator1 = "DDMEditStructure#FORM_FIELD_CONTAINER_LABEL");

		AssertTextEquals(
			key_fieldFieldLabel = "Text pt-BR",
			locator1 = "DDMEditStructure#SETTINGS_TEXT",
			value1 = "Text Help Text pt-BR");
	}

	@description = "This is a test for LPS-123539. As Content Creator, I want to leverage the structures’ translations with fildset when creating a new Web Content"
	@priority = "5"
	test CheckTranslationOfDuplicatedFieldsetOnWebContent {
		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Name");

		DataEngine.createNewFieldset(
			fieldNameList = "Text,Color",
			fieldsetName = "New Fieldset",
			localeList = "pt-BR");

		DataEngine.addFieldsets(fieldsetName = "New Fieldset");

		DataEngine.editFieldsetRepeatable(fieldsetLabel = "New Fieldset");

		PortletEntry.changeLocale(locale = "pt_BR");

		WebContentStructures.addName(structureName = "WC Structure Name pt-BR");

		WebContentStructures.saveCP();

		NavItem.click(navItem = "Web Content");

		WebContentNavigator.gotoAddWithStructureCP(structureName = "WC Structure Name");

		WebContent.addCP(webContentTitle = "Web Content Title");

		DataEngine.addRepeatableFieldset(fieldsetLabel = "New Fieldset");

		PortletEntry.changeLocale(locale = "pt_BR");

		WebContent.addCP(webContentTitle = "Web Content Title pt-BR");

		PortletEntry.publish();

		WebContentNavigator.gotoEditCP(webContentTitle = "Web Content Title");

		PortletEntry.changeLocale(locale = "pt_BR");

		AssertTextEquals(
			locator1 = "TextInput#TITLE",
			value1 = "Web Content Title pt-BR");

		for (var i : list "1,2") {
			AssertElementPresent(
				key_fieldFieldLabel = "New Fieldset pt-BR",
				key_index = "${i}",
				locator1 = "DDMEditStructure#FORM_FIELD_CONTAINER_LABEL_ANY");

			AssertElementPresent(
				key_fieldFieldLabel = "Text pt-BR",
				key_index = "${i}",
				locator1 = "DDMEditStructure#FORM_FIELD_CONTAINER_LABEL_ANY");

			AssertElementPresent(
				key_fieldFieldLabel = "Color pt-BR",
				key_index = "${i}",
				locator1 = "DDMEditStructure#FORM_FIELD_CONTAINER_LABEL_ANY");
		}
	}

	@description = "This is a test for LPS-123539. As Content Creator, I want to leverage the structures’ translations with fildset when creating a new Web Content"
	@ignore = "true"
	@priority = "4"
	test CheckTranslationOfFieldsetOnWebContent {
		property portal.acceptance = "false";

		// TODO LPS-123539 CheckTranslationOfFieldsetOnWebContent pending implementation
	}

	@description = "This is a test for LPS-123539. As Content Creator, I want to leverage the structures’ translations with fieldset when creating a new Web Content"
	@priority = "4"
	test DefaultTitleIsDisplayedUponLocaleChange {
		NavItem.gotoStructures();

		WebContentStructures.addCP(
			structureDescription = "Structure Description Test",
			structureName = "Structure FieldSet Test");

		DataEngine.addField(
			fieldFieldLabel = "Text Test",
			fieldName = "Text");

		WebContentStructures.saveCP(structureName = "Structure Name Test");

		WebContentStructures.addCP(
			structureDescription = "Structure Description Test",
			structureName = "Structure Name Test");

		DataEngine.addFieldsets(
			fieldsetLabel = "Structure FieldSet Test",
			fieldsetName = "Structure FieldSet Test");

		WebContentStructures.saveCP(structureName = "Structure Name Test");

		NavItem.gotoWebContent();

		WebContentNavigator.gotoAddWithStructureCP(structureName = "Structure Name Test");

		PortletEntry.inputTitle(title = "WC Title Test");

		PortletEntry.changeLocale(locale = "zh-CN");

		PortletEntry.inputTitle(title = "测试标题");

		PortletEntry.publish();

		WebContentNavigator.gotoEditCP(webContentTitle = "WC Title Test");

		PortletEntry.changeLocale(locale = "pt_BR");

		AssertTextEquals(
			locator1 = "TextInput#TITLE",
			value1 = "WC Title Test");

		PortletEntry.changeLocale(locale = "es_ES");

		AssertTextEquals(
			locator1 = "TextInput#TITLE",
			value1 = "WC Title Test");
	}

	@description = "This is a test for LPS-126217. Check when user translates the title, the default language title persists"
	@priority = "4"
	test DefaultTitlePersistsAfterTranslatingForAnotherLocale {
		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Title");

		DataEngine.gotoCreateNewFieldset();

		Type(
			locator1 = "FormViewBuilder#FIELD_SET_NAME",
			value1 = "Fieldset Default Title");

		DataEngine.addFields(fieldNameList = "Text");

		PortletEntry.changeLocale(
			locale = "zh-CN",
			modal = "true");

		Type(
			locator1 = "FormViewBuilder#FIELD_SET_NAME",
			value1 = "测试标题");

		Click(locator1 = "Modal#SAVE_BUTTON");

		DataEngine.addFieldsets(fieldsetName = "Fieldset Default Title");

		WebContentStructures.saveCP();

		NavItem.click(navItem = "Web Content");

		WebContentNavigator.gotoAddWithStructureCP(structureName = "WC Structure Title");

		WebContent.addCP(webContentTitle = "Web Content Title");

		PortletEntry.publish();

		WebContentNavigator.gotoEditCP(webContentTitle = "Web Content Title");

		AssertVisible(
			key_fieldsetLabel = "Fieldset Default Title",
			locator1 = "DDMEditStructure#FORM_FIELD_SETS_CONTAINER_LABEL");
	}

	@description = "This is a test for LPS-123539. As Content Creator, I want to leverage the structures’ translations with fildset when creating a new Web Content"
	@priority = "4"
	test DefaultTitlePersistsUponFirstlyTranslatingForAnotherLocale {
		NavItem.gotoStructures();

		WebContentStructures.addCP(
			structureDescription = "Structure Description Test",
			structureName = "Structure FieldSet Test");

		DataEngine.addField(
			fieldFieldLabel = "Text Test",
			fieldName = "Text");

		WebContentStructures.saveCP(structureName = "Structure Name Test");

		WebContentStructures.addCP(
			structureDescription = "Structure Description Test",
			structureName = "Structure Name Test");

		DataEngine.addFieldsets(
			fieldsetLabel = "Structure FieldSet Test",
			fieldsetName = "Structure FieldSet Test");

		WebContentStructures.saveCP(structureName = "Structure Name Test");

		NavItem.gotoWebContent();

		WebContentNavigator.gotoAddWithStructureCP(structureName = "Structure Name Test");

		PortletEntry.inputTitle(title = "WC Title Test");

		PortletEntry.changeLocale(locale = "pt-BR");

		PortletEntry.inputTitle(title = "BR Title");

		PortletEntry.changeLocale(locale = "en-US");

		PortletEntry.inputTitle(title = "EN Title");

		PortletEntry.publish();

		WebContentNavigator.gotoEditCP(webContentTitle = "EN Title");

		PortletEntry.changeLocale(locale = "pt-BR");

		AssertTextEquals(
			locator1 = "TextInput#TITLE",
			value1 = "BR Title");
	}

	@description = "This is a test for LPS-123539. As Content Creator, I want to leverage the structures’ translations with fildset when creating a new Web Content"
	@ignore = "true"
	@priority = "5"
	test TranslateWebContentFromStructureWithFieldset {
		property portal.acceptance = "false";

		// TODO LPS-123539 TranslateWebContentFromStructureWithFieldset pending implementation
	}

	@description = "This is a test for LPS-123539. As Content Creator, I want to leverage the structures’ translations with fildset when creating a new Web Content"
	@priority = "5"
	test TranslateWebContentFromStructureWithFieldsetWithFieldGroup {
		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Name");

		DataEngine.gotoCreateNewFieldset();

		Type(
			locator1 = "FormViewBuilder#FIELD_SET_NAME",
			value1 = "New Fieldset");

		DataEngine.addFields(fieldNameList = "Text");

		DataEngine.addFieldNested(
			fieldFieldLabel = "Rich Text",
			fieldName = "Rich Text",
			targetFieldLabel = "Text");

		DataEngine.editFieldTip(
			fieldFieldLabel = "Text",
			fieldHelp = "Text Field Help Text",
			fieldsetModal = "true");

		DataEngine.editFieldTip(
			fieldFieldLabel = "Rich Text",
			fieldHelp = "Rich Text Field Help Text",
			fieldsetModal = "true");

		PortletEntry.changeLocale(
			locale = "pt-BR",
			modal = "true");

		Type(
			locator1 = "FormViewBuilder#FIELD_SET_NAME",
			value1 = "Novo Fieldset");

		DataEngine.editFieldsGroupLabel(
			fieldsetModal = "true",
			fieldsGroupLabel = "Fields Group",
			fieldsGroupLabelEdit = "Grupo de Campos");

		DataEngine.editFieldLabel(
			fieldFieldLabel = "Text",
			fieldFieldLabelEdit = "Texto",
			fieldsetModal = "true");

		DataEngine.editFieldLabel(
			fieldFieldLabel = "Rich Text",
			fieldFieldLabelEdit = "Rico Texto",
			fieldsetModal = "true");

		DataEngine.editFieldTip(
			fieldFieldLabel = "Texto",
			fieldHelp = "Texto de ajuda do campo de texto",
			fieldsetModal = "true");

		DataEngine.editFieldTip(
			fieldFieldLabel = "Rico Texto",
			fieldHelp = "Texto de ajuda do campo Rico Texto",
			fieldsetModal = "true");

		Click(locator1 = "Modal#SAVE_BUTTON");

		Alert.viewSuccessMessage();

		DataEngine.addFieldsets(fieldsetName = "New Fieldset");

		WebContentStructures.saveCP();

		NavItem.click(navItem = "Web Content");

		WebContentNavigator.gotoAddWithStructureCP(structureName = "WC Structure Name");

		WebContent.addCP(webContentTitle = "Web Content Title");

		PortletEntry.publish();

		WebContentNavigator.gotoEditCP(webContentTitle = "Web Content Title");

		AssertTextEquals(
			locator1 = "TextInput#TITLE",
			value1 = "Web Content Title");

		AssertVisible(
			key_fieldsetLabel = "New Fieldset",
			locator1 = "DDMEditStructure#FORM_FIELD_SETS_CONTAINER_LABEL");

		AssertVisible(
			key_fieldFieldLabel = "Fields Group",
			locator1 = "DDMEditStructure#FORM_FIELD_CONTAINER_LABEL");

		for (var fieldFieldLabel : list "Text,Rich Text") {
			AssertVisible(
				key_fieldFieldLabel = "${fieldFieldLabel}",
				key_fieldsGroupLabel = "Fields Group",
				locator1 = "DataEngineBuilder#FORM_FIELD_NESTED");
		}

		for (var fieldFieldLabel : list "Text,Rich Text") {
			AssertTextEquals(
				key_fieldFieldLabel = "${fieldFieldLabel}",
				locator1 = "DDMEditStructure#SETTINGS_TEXT",
				value1 = "${fieldFieldLabel} Field Help Text");
		}

		PortletEntry.changeLocale(locale = "pt-BR");

		AssertTextEquals(
			locator1 = "TextInput#TITLE",
			value1 = "Web Content Title");

		AssertVisible(
			key_fieldsetLabel = "Novo Fieldset",
			locator1 = "DDMEditStructure#FORM_FIELD_SETS_CONTAINER_LABEL");

		AssertVisible(
			key_fieldFieldLabel = "Grupo de Campos",
			locator1 = "DDMEditStructure#FORM_FIELD_CONTAINER_LABEL");

		for (var fieldFieldLabel : list "Texto,Rico Texto") {
			AssertVisible(
				key_fieldFieldLabel = "${fieldFieldLabel}",
				key_fieldsGroupLabel = "Grupo de Campos",
				locator1 = "DataEngineBuilder#FORM_FIELD_NESTED");
		}

		AssertTextEquals(
			key_fieldFieldLabel = "Texto",
			locator1 = "DDMEditStructure#SETTINGS_TEXT",
			value1 = "Texto de ajuda do campo de texto");

		AssertTextEquals(
			key_fieldFieldLabel = "Rico Texto",
			locator1 = "DDMEditStructure#SETTINGS_TEXT",
			value1 = "Texto de ajuda do campo Rico Texto");
	}

	@description = "This is a test for LPS-123539. As Content Creator, I want to leverage the structures’ translations with fieldset when creating a new Web Content"
	@priority = "5"
	test TranslationPersistsAfterEditingAnother {
		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Name");

		DataEngine.addFieldsets(
			fieldsetLabel = "Basic Web Content",
			fieldsetName = "Basic Web Content");

		WebContentStructures.saveCP();

		NavItem.click(navItem = "Web Content");

		WebContentNavigator.gotoAddWithStructureCP(structureName = "WC Structure Name");

		WebContent.addCP(
			webContentContent = "Web Content Content",
			webContentTitle = "Web Content Title");

		PortletEntry.changeLocale(locale = "pt_BR");

		WebContent.addCP(
			webContentContent = "Conteúdo da web",
			webContentTitle = "Título do conteúdo da web");

		PortletEntry.changeLocale(locale = "es_ES");

		WebContent.addCP(
			webContentContent = "Contenido de contenido web",
			webContentTitle = "Título del contenido web");

		PortletEntry.publish();

		WebContentNavigator.gotoEditCP(webContentTitle = "Web Content Title");

		WebContent.viewCP(
			webContentContent = "Web Content Content",
			webContentTitle = "Web Content Title");

		PortletEntry.changeLocale(locale = "pt_BR");

		WebContent.viewCP(
			webContentContent = "Conteúdo da web",
			webContentTitle = "Título do conteúdo da web");

		PortletEntry.changeLocale(locale = "es_ES");

		WebContent.viewCP(
			webContentContent = "Contenido de contenido web",
			webContentTitle = "Título del contenido web");
	}

	@description = "This is a test for LPS-123539. As Content Creator, I want to leverage the structures’ translations with fildset when creating a new Web Content"
	@ignore = "true"
	@priority = "4"
	test VerifyDefaultLanguageIsFirstShown {
		property portal.acceptance = "false";

		// TODO LPS-123539 VerifyDefaultLanguageIsFirstShown pending implementation
	}

}